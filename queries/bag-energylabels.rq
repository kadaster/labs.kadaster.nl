prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix energylabels: <https://data.labs.pdok.nl/def/energielabels/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix graph: <https://data.labs.pdok.nl/graph/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
select * {
  {
    select * {
      ?openbareRuimte
        bag:naamOpenbareRuimte "Diezerstraat";
        bag:bijbehorendeWoonplaats ?woonplaats.
      ?woonplaats bag:naamWoonplaats "Zwolle".
      ?nummeraanduiding
        bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
        bag:huisnummer ?huisnummer;
        bag:postcode ?postcode.
      ?verblijfsobject
        bag:hoofdadres ?nummeraanduiding;
        bag:oppervlakte ?oppervlakte;
        bag:pandrelatering ?pand;
        bag:status ?verblijfsobjectStatus.
      ?verblijfsobjectStatus rdfs:label ?verblijfsobjectStatusLabel.
      ?pand
        bag:oorspronkelijkBouwjaar ?bouwjaar;
        bag:status ?pandStatus;
        geo:hasGeometry/geo:asWKT ?shape.
      ?pandStatus rdfs:label ?pandStatusLabel.
    }
  }
  bind(str(?huisnummer) as ?huisnummerString)
  service <https://data.labs.pdok.nl/sparql> {
    graph graph:energylabels {
      optional {
        [ energylabels:ep ?ep;
          energylabels:e_label ?energylabel;
          energylabels:opname_datum ?opnamedatum;
          energylabels:pand_huisnummer ?huisnummerString;
          energylabels:pand_postcode ?postcode;
          energylabels:registratie_datum ?registratiedatum ]
      }
    }
  }
  bind(if(!(bound(?energylabel)),'grey',
        if(strstarts(?energylabel,'A'),'#22b14c', # Also covers A+, A++, and A+++.
        if(?energylabel='B','#8ff334',
        if(?energylabel='C','#bdfc2c',
        if(?energylabel='D','#fff200',
        if(?energylabel='E','#ff9a35',
        if(?energylabel='F','#ff7f27',
        if(?energylabel='G','#ed1c24',
        'grey')))))))) as ?shapeColor_)
  bind(if(bound(?x),concat('<h5>Energielabel informatie:</h5><ul><li>Energielabel: ',str(?energylabel),'</li><li>EP: ',str(?ep),'</li><li>Opnamedatum: ',str(?opnamedatum),'</li><li>Registratiedatum: ',str(?registratiedatum),'</li></ul>'),'') as ?energyHtml)
  bind('''
<h4>
  <svg height="20" viewBox="0 0 20 20" xmlns="http://www.w3.org/2000/svg">
    <circle cx="10" cy="10" fill="{{shapeColor_}}" r="10"/>
  </svg>
  Diezerstraat {{huisnummer}}
</h4>
{{energyHtml}}
<h5><a href="{{verblijfsobject}}">Verblijfsobject</a>:</h5>
<ul>
  <li>Woonplaats: <a href="{{woonplaats}}">Zwolle</a></li>
  <li>Oppervlakte: {{oppervlakte}}mÂ²</li>
  <li>Status: <a href="{{verblijfsobjectStatus}}">{{verblijfsobjectStatusLabel}}</a></li>
</ul>
<h5><a href="{{pand}}">Pand</a>:</h5>
<ul>
  <li>Bouwjaar: {{bouwjaar}}</li>
  <li>Status: <a href="{{pandStatus}}">{{pandStatusLabel}}</a></li>
</ul>'''^^rdf:HTML as ?shapeLabel_)
}
group by ?shape
limit 100
